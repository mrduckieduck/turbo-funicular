plugins {
    id "groovy"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "io.micronaut.application" version '1.4.5'
    id "idea"
    id "com.github.ben-manes.versions" version "0.38.0"
    id 'jacoco'
    id 'com.google.cloud.tools.jib' version '3.0.0'
}

version "0.0.1"
group "turbo.funicular"

ext {
    registryUsername = System.getenv()['DOCKERHUB_USER'] ?: ''
    registryPassword = System.getenv()['DOCKERHUB_PASS'] ?: ''
    imageTagSuffix = System.getenv()['CIRCLE_BUILD_NUM'] ?: 'snapshot'
    javaMem = '-Xmx128m'
}

repositories {
    mavenCentral()
}

micronaut {
    version micronautVersion
    runtime "netty"
    testRuntime "spock2"
    processing {
        incremental true
        annotations "org.*"
        annotations "com.*"
    }
}

dependencies {
    annotationProcessor("io.micronaut.data:micronaut-data-processor:${ micronautDataVersion }")
    //THE ORDER MATTERS FOR THIS TREE MAPSTRUCT SHOULD BE BEFORE OF LOMBOK ALWAYS
    annotationProcessor "org.mapstruct:mapstruct-processor:${ mapstructVersion }"
    annotationProcessor("org.projectlombok:lombok:${ lombokVersion }")
    annotationProcessor("org.projectlombok:lombok-mapstruct-binding:0.2.0")
    //
    annotationProcessor("io.micronaut.security:micronaut-security-annotations")

    compileOnly("org.graalvm.nativeimage:svm")
    compileOnly("org.projectlombok:lombok:${ lombokVersion }")
    compileOnly("jakarta.persistence:jakarta.persistence-api:2.2.2")

    implementation("org.mapstruct:mapstruct:${ mapstructVersion }")
    implementation("io.micronaut:micronaut-validation")
    implementation("io.micronaut:micronaut-runtime")
    implementation("javax.annotation:javax.annotation-api")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut.flyway:micronaut-flyway:${ micronautFlywayVersion }")
    implementation "io.micronaut.sql:micronaut-jdbc-hikari:${ micronautJdbcHikari }", {
        version { strictly "${ micronautJdbcHikari }" }
    }
    implementation("io.micronaut.data:micronaut-data-jdbc:${ micronautDataVersion }")
    implementation("org.kohsuke:github-api:1.127")
    implementation("org.eclipse.mylyn.github:org.eclipse.egit.github.core:2.1.5")

    implementation("io.micronaut.security:micronaut-security-jwt")
    implementation("io.micronaut.security:micronaut-security-oauth2")
    implementation "io.micronaut.views:micronaut-views-thymeleaf:${ micronautViewsThymeleaf }", {
        version { strictly "${ micronautViewsThymeleaf }" }
    }
    implementation("com.google.guava:guava:30.1.1-jre")

    implementation("io.vavr:vavr:1.0.0-alpha-3")

    testImplementation "org.codehaus.groovy:groovy:${ groovyVersion }", {
        version { strictly "${ groovyVersion }" }
    }
    testImplementation "org.spockframework:spock-core:${ spockVersion }", {
        version { strictly "${ spockVersion }" }
    }
    testImplementation "io.micronaut.test:micronaut-test-spock:${ micronautTestSpock }", {
        version { strictly "${ micronautTestSpock }" }
    }

    testImplementation "org.gebish:geb-spock:${ gebVersion }"
    //testImplementation "org.seleniumhq.selenium:selenium-firefox-driver:${ seleniumVersion }"
    testImplementation "org.seleniumhq.selenium:htmlunit-driver:${ seleniumHtmlUnitDriverVersion }"
    testImplementation "org.seleniumhq.selenium:selenium-api:${ seleniumVersion }"
    testImplementation "org.seleniumhq.selenium:selenium-support:${ seleniumVersion }"
    testRuntimeOnly "net.sourceforge.htmlunit:htmlunit:2.49.1"

    implementation 'com.github.javafaker:javafaker:1.0.2'
    implementation 'io.vavr:vavr:0.10.3'

    runtimeOnly("ch.qos.logback:logback-classic")
    runtimeOnly("org.postgresql:postgresql")

}

application {
    mainClass.set("turbo.funicular.Application")
}

java {
    sourceCompatibility = JavaVersion.toVersion(javaVersion)
    targetCompatibility = JavaVersion.toVersion(javaVersion)
}

test {
    systemProperties "geb.build.reportsDir": "$reportsDir/geb"
}

//This config seems not be working well, that's the reason we are preferring for now jib instead this functionality from Micronaut Gradle plugin
docker {
    registryCredentials {
        username = registryUsername
        password = registryPassword
    }
}

dockerfile {
    args(javaMem)
}

dockerBuild {
    images = ["$imageOwner/$imageName:$project.version".toLowerCase()]
}

jib {
    from {
        image = 'openjdk:15-alpine'
    }
    to {
        image = "$imageOwner/$imageName:${ project.version }_${ imageTagSuffix }".toLowerCase()
        auth {
            username = registryUsername
            password = registryPassword
        }
    }
    container {
        jvmFlags = [javaMem, '-Xdebug']
        ports = ['8080']
        format = 'OCI'
    }
}

jacoco {
    toolVersion = jacocoVersion
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.3
            }
        }
    }
}
